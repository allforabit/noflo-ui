<!DOCTYPE html>
<html>
  <head>
    <meta charset="utf-8">

    <title>NoFlo Development Environment</title>
    <link rel="shortcut icon" href="favicon.ico" />

    <script src="bower_components/polymer/polymer.min.js"></script>
    <link rel="import" href="bower_components/the-behavior/the-behaviors/the-behaviors.html">
    <link rel="import" href="bower_components/the-panel/the-panel/the-panel.html">
    <link rel="import" href="bower_components/the-graph/the-graph-editor/the-graph-editor.html">
    <noflo-polymer name="the-graph-editor" inports="graph width height" outports="changed nodeclick edgeclick"></noflo-polymer>
    <link rel="import" href="browser/noflo-noflo-polymer/noflo-polymer/noflo-polymer.html">
    <link rel="import" href="elements/noflo-context.html">
    <noflo-polymer name="noflo-context" inports="project graphs component node edge buttons search clear" outports="project graphs currentgraph component nodes edges button result"></noflo-polymer>
    <link rel="import" href="elements/noflo-main.html">
    <noflo-polymer name="noflo-main" inports="projects sketches examples" outports="newsketch newproject"></noflo-polymer>
    <link rel="import" href="elements/noflo-runtime.html">
    <noflo-polymer name="noflo-runtime" inports="graphs" outports="runtime"></noflo-polymer>
    <link rel="import" href="elements/noflo-breadcrumb.html">
    <noflo-polymer name="noflo-breadcrumb" inports="project graphs" outports=""></noflo-polymer>
    <link rel="import" href="elements/noflo-search.html">
    <noflo-polymer name="noflo-search" inports="buttons results" outports="menu search"></noflo-polymer>
    <link rel="import" href="elements/noflo-library.html">
    <noflo-polymer name="noflo-library" inports="buttons editor search" outports="button result"></noflo-polymer>
    <link rel="import" href="elements/noflo-account.html">
    <link rel="import" href="elements/noflo-project.html">
    <noflo-polymer name="noflo-project" inports="project graphs" outports=""></noflo-polymer>
    <link rel="stylesheet" href="bower_components/font-awesome/css/font-awesome.min.css">

    <!-- NoFlo and NoFlo UI -->
    <script src="./browser/noflo-ui.js"></script>

    <!-- NoFlo UI Style -->
    <link rel="stylesheet" href="./css/noflo-ui.css">

    <meta name="viewport" content="width=device-width, initial-scale=1.0, maximum-scale=1.0, minimum-scale=1.0, user-scalable=no" />
  </head>
  <body>
    <noflo-context></noflo-context>
    <noflo-runtime></noflo-runtime>
    <noflo-breadcrumb></noflo-breadcrumb>
    <noflo-search></noflo-search>
    <noflo-library></noflo-library>
    <the-panel id="context" edge="left" size="280">
      <header></header>
      <main></main>
      <footer></footer>
    </the-panel>
    <noflo-project></noflo-project>
    <main id="workspace"></main>
    <the-panel id="fixed" edge="right" size="280" handle="35" automatic="false">
      <header></header>
      <main></main>
      <footer></footer>
    </the-panel>
    <script type="application/fbp" id="LoadList">
      EXPORT=LISTKEY.STRING:KEY
      EXPORT=MERGESTART.IN:LOAD
      EXPORT=GETLIST.ITEMS:ITEMS
      ListKey(strings/SendString) OUT -> KEY GetList(localstorage/ListGet)
      MergeStart(core/Merge) OUT -> IN ListKey
    </script>
    <script type="application/fbp" id="CreateItem">
      EXPORT=ADD.LIST:KEY
      EXPORT=SPLITITEM.IN:OBJECT
      EXPORT=HOLDNAME.OUT:STORED
      SplitItem(core/Split) OUT -> START CreateId(ui/GenerateId)
      SplitItem OUT -> IN ToJson(strings/Jsonify)
      CreateId OUT -> IN SplitName(core/Split)
      SplitName OUT -> KEY Add(localstorage/ListAdd)
      Add KEY -> KEY Save(localstorage/SetItem)
      ToJson OUT -> VALUE Save
      SplitName OUT -> DATA HoldName(core/Kick)
      Save ITEM -> IN HoldName
    </script>
    <script type="application/fbp" id="PrepareProject">
      EXPORT=GETTYPE.IN:PROJECT
      EXPORT=SETMAIN.OUT:PROJECT
      # Prepare and store a 'main graph'
      'type' -> KEY GetType(objects/GetObjectKey)
      GetType OUT -> IN SplitType(core/Split)
      'type' -> PROPERTY SetType(objects/SetPropertyValue)
      SplitType OUT -> VALUE SetType
      SplitType OUT -> START GenerateId(ui/GenerateId) OUT -> IN SplitId(core/Split)
      SplitType OUT -> START CreateDetails(objects/CreateObject)
      CreateDetails OUT -> IN SetType OUT -> DETAILS CreateGraph(ui/CreateGraph)
      SplitId OUT -> KEY StoreGraph(localstorage/SetItem)
      CreateGraph OUT -> IN ToJson(strings/Jsonify) OUT -> VALUE StoreGraph
      'main' -> PROPERTY SetMain(objects/SetPropertyValue)
      SplitId OUT -> VALUE SetMain
      'graphs' -> KEY GetGraphs(objects/GetObjectKey)
      GetType OBJECT -> IN GetGraphs OBJECT -> IN SetMain
      GetGraphs OUT -> TO AddGraph(ui/AppendTo)
      SplitId OUT -> APPEND AddGraph
    </script>
    <script type="application/fbp" id="SaveItem">
      EXPORT=GETGRAPHID.IN:ITEM
      'graphId' -> KEY GetGraphId(objects/GetObjectKey)
      GetGraphId OUT -> IN Id(core/Merge)
      GetGraphId OBJECT -> IN Object(core/Merge)
      'id' -> KEY GetId(objects/GetObjectKey)
      GetGraphId MISSED -> IN GetId
      GetId OUT -> IN Id
      GetId OBJECT -> IN Object
      Id OUT -> KEY Store(localstorage/SetItem)
      Object OUT -> IN ToJson(strings/Jsonify) OUT -> VALUE Store
    </script>
    <script type="application/fbp" id="LoadItem">
      EXPORT=LOADSTORED.KEY:KEY
      EXPORT=SETID.OUT:OUT
      EXPORT=LOADSTORED.ERROR:ERROR
      LoadStored(localstorage/GetItem) ITEM -> IN GetId(groups/ReadGroup)
      'id' -> PROPERTY SetId
      GetId GROUP -> IN SplitId(core/Split) OUT -> VALUE SetId(objects/SetPropertyValue)
      GetId OUT -> STRING HoldItem(strings/SendString) OUT -> IN Parse(strings/ParseJson)
      SplitId OUT -> IN HoldItem
      Parse OUT -> IN SetId
    </script>
    <script type="application/fbp" id="GraphEditor">
      EXPORT=LOADEDITOR.CONTAINER:CONTAINER
      EXPORT=CONNECTRUNTIME.RUNTIME:RUNTIME
      EXPORT=MERGEGRAPH.IN:CURRENTGRAPH
      EXPORT=SPLITGRAPH.OUT:CURRENTGRAPH
      EXPORT=LOADEDITOR.EDITOR:EDITOR
      EXPORT=GRAPHEDITOR.NODECLICK:NODECLICK
      EXPORT=GRAPHEDITOR.EDGECLICK:EDGECLICK
      EXPORT=GRAPHEDITOR.CHANGED:CHANGED
      MergeGraph(core/Merge) OUT -> IN SplitGraph(core/Split) OUT -> GRAPH LoadEditor(ui/LoadGraphEditor)
      LoadEditor EDITOR -> EDITOR ConnectRuntime(ui/ConnectRuntime)
      ConnectRuntime EDITOR -> ELEMENT GraphEditor(polymer/the-graph-editor)
    </script>
    <script type="application/fbp" id="LoadGist">
      EXPORT=SPLITGIST.IN:ID
      EXPORT=PARSE.OUT:OUT
      'https://api.github.com/gists' -> STRING GistPrefix(strings/SendString)
      '/' -> DELIMITER CreateUrl(strings/CompileString)
      SplitGist(core/Split) OUT -> IN GistPrefix
      GistPrefix OUT -> IN CreateUrl
      SplitGist OUT -> IN CreateUrl
      CreateUrl OUT -> URL GetGist(ajax/GetJsonP)
      'data' -> KEY GetData(objects/GetObjectKey)
      'files' -> KEY GetFiles(objects/GetObjectKey)
      'noflo.json' -> KEY GetFile(objects/GetObjectKey)
      'content' -> KEY GetFileContent(objects/GetObjectKey)
      GetGist OUT -> IN GetData
      GetData OUT -> IN GetFiles OUT -> IN GetFile OUT -> IN GetFileContent
      GetFiles MISSED -> IN Errors
      GetFileContent OUT -> IN Parse(strings/ParseJson)
    </script>
    <script type="application/fbp" id="MainView">
      EXPORT=CONTAINER.DATA:CONTAINER
      EXPORT=CONTAINER.IN:LOAD
      EXPORT=LOADMAIN.SKETCHES:SKETCHES
      EXPORT=LOADMAIN.PROJECTS:PROJECTS
      EXPORT=MAINISREADY.OUT:READY
      EXPORT=LOADMAIN.NEWSKETCH:NEWSKETCH
      EXPORT=LOADMAIN.NEWPROJECT:NEWPROJECT
      'noflo-main' -> TAGNAME CreateMain(dom/CreateElement)
      Container(core/Kick) OUT -> CONTAINER CreateMain
      CreateMain ELEMENT -> ELEMENT LoadMain(polymer/noflo-main)
      LoadMain ELEMENT -> IN MainIsReady(core/Split)

      # Load examples
      'examples.json' -> STRING ExampleUrl(strings/SendString)
      MainIsReady OUT -> IN ExampleUrl
      ExampleUrl OUT -> URL GetExamples(ajax/Get)
      GetExamples OUT -> IN ParseExamples(strings/ParseJson)
      'examples' -> KEY GetExampleList(objects/GetObjectKey)
      ParseExamples OUT -> IN GetExampleList OUT -> EXAMPLES LoadMain
    </script>
    <script type="application/fbp" id="main">
      # Key for stored sketches
      'noflo-ui-graphs' -> IN GraphKey(core/Split)
      # Key for stored projects
      'noflo-ui-projects' -> IN ProjectKey(core/Split)

      # NoFlo context
      'noflo-context' -> SELECTOR GetContext(dom/GetElement)
      GetContext ELEMENT -> ELEMENT Context(polymer/noflo-context)

      # Body for class handling
      'body' -> SELECTOR GetBody(dom/GetElement) ELEMENT -> IN SplitBody(core/Split)

      # Runtime handling
      'noflo-runtime' -> SELECTOR GetRuntime(dom/GetElement)
      GetRuntime ELEMENT -> ELEMENT Runtime(polymer/noflo-runtime)
      Context GRAPHS -> GRAPHS Runtime

      # Breadcrumb handling
      'noflo-breadcrumb' -> SELECTOR GetBreadcrumb(dom/GetElement)
      GetBreadcrumb ELEMENT -> ELEMENT Breadcrumb(polymer/noflo-breadcrumb)
      Context GRAPHS -> GRAPHS Breadcrumb
      Context PROJECT -> PROJECT Breadcrumb

      # Project panel handling
      'noflo-project' -> SELECTOR GetProject(dom/GetElement)
      GetProject ELEMENT -> ELEMENT Project(polymer/noflo-project)
      Context PROJECT -> PROJECT Project
      'graphs' -> KEY GetProjectGraphs(objects/GetObjectKey)
      GetProjectGraphs MISSED -> IN IgnoreEmptyProjects(core/Drop)
      Context PROJECT -> IN GetProjectGraphs OUT -> IN SplitGraphList(objects/SplitArray)
      SplitGraphList OUT -> KEY LoadProjectGraph(local/LoadItem) OUT -> GRAPHS Project

      # NoFlo search
      'noflo-search' -> SELECTOR GetSearch(dom/GetElement) ELEMENT -> ELEMENT Search(polymer/noflo-search)

      # NoFlo component library
      'noflo-library' -> SELECTOR GetLibrary(dom/GetElement) ELEMENT -> ELEMENT Library(polymer/noflo-library)

      # UI main menu
      Search MENU -> BUTTONS Library BUTTON -> BUTTONS Search
      Search MENU -> BUTTONS Context BUTTON -> BUTTONS Search

      # UI searching
      Search SEARCH -> SEARCH Library RESULT -> RESULTS Search
      Search SEARCH -> SEARCH Context RESULT -> RESULTS Search

      # The DOM elements where the UI will be run
      '#workspace' -> SELECTOR GetWorkspaceContainer(dom/GetElement)
      GetWorkspaceContainer ELEMENT -> IN SplitWorkspaceContainer(core/Split)

      # Workspace clearing on route changes
      '' -> DATA DoClearWorkspace(core/Kick)
      Router(ui/Router) ROUTE -> IN DoClearWorkspace
      SplitWorkspaceContainer OUT -> CONTAINER ClearWorkspace(dom/WriteHtml)
      DoClearWorkspace OUT -> HTML ClearWorkspace

      # ## Main view
      # Main view listing user's projects and examples
      SplitWorkspaceContainer OUT -> CONTAINER LoadMain(local/MainView)
      Router CLEAR -> CLEAR Context
      Router(ui/Router) MAIN -> LOAD LoadMain

      # Load local sketches
      GraphKey OUT -> KEY LoadSketches(local/LoadList)
      LoadMain READY -> LOAD LoadSketches
      LoadSketches ITEMS -> KEY LoadSketch(local/LoadItem) OUT -> SKETCHES LoadMain
      LoadSketch ERROR -> IN DropErrors(core/Drop)

      # Load local projects
      ProjectKey OUT -> KEY LoadProjects(local/LoadList)
      LoadMain READY -> LOAD LoadProjects
      'main' -> KEY GetMainGraph(objects/GetObjectKey)
      LoadProjects ITEMS -> KEY LoadProjectItem(local/LoadItem) OUT -> IN GetMainGraph
      GetMainGraph OUT -> KEY LoadProjectMain(local/LoadItem)
      'mainGraph' -> PROPERTY SetMainGraph(objects/SetPropertyValue)
      LoadProjectMain OUT -> VALUE SetMainGraph
      GetMainGraph OBJECT -> IN SetMainGraph OUT -> PROJECTS LoadMain
      LoadProjectItem ERROR -> IN DropErrors(core/Drop)

      # Save newly created graphs into localStorage
      LoadMain NEWSKETCH -> DETAILS CreateGraph(ui/CreateGraph)
      GraphKey OUT -> KEY SaveGraph(local/CreateItem)
      CreateGraph OUT -> OBJECT SaveGraph(local/CreateItem)

      # Save newly created projects into localStorage
      ProjectKey OUT -> KEY SaveProject(local/CreateItem)
      LoadMain NEWPROJECT -> PROJECT PrepareProject(local/PrepareProject)
      PrepareProject PROJECT -> OBJECT SaveProject(local/CreateItem)
      SaveProject STORED -> LOAD LoadProjects

      # Redirect to graph
      'graph' -> STRING PrepareGraphUri(strings/SendString)
      '/' -> DELIMITER CreateGraphUri(strings/CompileString)
      SplitNewJson OUT -> IN PrepareGraphUri
      PrepareGraphUri OUT -> IN CreateGraphUri
      SaveGraph STORED -> IN CreateGraphUri
      CreateGraphUri OUT -> HASH Navigate(interaction/SetHash)

      # ## Project loading
      Router PROJECT -> KEY LoadProject(local/LoadItem)
      LoadProject OUT -> PROJECT Context
      LoadProject ERROR -> IN Errors

      # ## Graph loading
      Router GRAPH -> KEY LoadStored(local/LoadItem)
      LoadStored OUT -> GRAPHS Context
      LoadStored ERROR -> IN Errors

      # Prepare the-graph in DOM
      SplitWorkspaceContainer OUT -> CONTAINER EditGraph(local/GraphEditor)
      # Context binding
      Context CURRENTGRAPH -> CURRENTGRAPH EditGraph
      Runtime RUNTIME -> RUNTIME EditGraph
      EditGraph NODECLICK -> NODE Context
      EditGraph EDGECLICK -> EDGE Context
      EditGraph EDITOR -> EDITOR Library

      # Listen to changes and save to localStorage
      EditGraph CHANGED -> ITEM Store(local/SaveItem)

      # Example gist loading
      Router EXAMPLE -> ID LoadGist(local/LoadGist)
      LoadGist OUT -> GRAPHS Context

      # Routing
      '' -> START ListenHash(interaction/ListenHash)
      ListenHash INITIAL -> IN MergeUrl(core/Merge)
      ListenHash CHANGE -> IN MergeUrl
      MergeUrl OUT -> URL Router

      # Invalid route handling
      '' -> DATA GoToMain(core/Kick)
      Router MISSED -> IN Errors(core/Merge)
      Errors(core/Merge) OUT -> IN GoToMain
      GoToMain OUT -> HASH Navigate(interaction/SetHash)

      # Add or remove the 'main' class from the body
      SplitBody OUT -> STRING SendRemoveClass(strings/SendString) OUT -> ELEMENT RemoveClass(dom/RemoveClass)
      SplitBody OUT -> STRING SendAddClass(strings/SendString) OUT -> ELEMENT AddClass(dom/AddClass)
      'main' -> CLASS RemoveClass
      'main' -> CLASS AddClass
      Router ROUTE -> IN SendRemoveClass
      LoadMain READY -> IN SendAddClass
    </script>
    <script>
    window.addEventListener('WebComponentsReady', function() { 
      var noflo = require('noflo');
      var graphs = {};
      var fbps = Array.prototype.slice.call(document.querySelectorAll('script[type="application/fbp"]'));
      fbps.forEach(function (fbp) {
        var fbpString = (fbp.innerText || fbp.textContent).trim();
        var fbpId = fbp.getAttribute('id');
        noflo.graph.loadFBP(fbpString, function (graph) {
          graphs[fbpId] = graph;
          graph.name = fbpId;
          graph.baseDir = '/noflo-ui';
          if (fbpId === 'main') {
            noflo.createNetwork(graph, function (network) {
              for (var component in graphs) {
                if (component === 'main') {
                  continue;
                }
                network.loader.registerComponent('local', component, graphs[component]);
              }
              network.connect(function () {
                network.start();
              });
            }, true);
          }
        });
      });
    });
    </script>
  </body>
</html>
